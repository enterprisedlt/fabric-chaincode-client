plugins {
    id 'scala'
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

group 'org.enterprisedlt.fabric'
version '1.4.3.200-RC3'

compileScala.targetCompatibility = 1.8

dependencies {
    compile 'org.scala-lang:scala-library:2.12.0'
    compile('org.hyperledger.fabric-sdk-java:fabric-sdk-java:1.4.3') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    // java chain code uses different logger facades, namely JUL and commons logging ...
    compile 'org.slf4j:jcl-over-slf4j:1.7.25'
    compile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile 'org.enterprisedlt.general:codecs:2.0.0-RC2'


    // testing libraries
    testCompile 'junit:junit:4.12'
    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
    testCompile 'org.mockito:mockito-core:2.23.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/enterprisedlt/general' }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: [javadoc, scaladoc]) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId group
            artifactId rootProject.name
            version version
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    publish = true
    pkg {
        repo = 'fabric'
        userOrg = 'enterprisedlt'
        name = 'fabric-chaincode-client'
        websiteUrl = 'https://github.com/enterprisedlt/fabric-chaincode-client'
        vcsUrl = 'https://github.com/enterprisedlt/fabric-chaincode-client'
        publicDownloadNumbers = true
        licenses = ['LGPL-3.0']
        githubRepo = 'enterprisedlt/fabric-chaincode-client'
        version {
            name = project.version
            desc = project.version
            released = new Date()
        }
    }
}
